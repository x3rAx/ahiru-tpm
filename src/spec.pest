WHITESPACE = _{ " " | "\t" }

spec      = _{ SOI ~ url ~ attributes? ~ EOI }
url       = ${ (short_url | full_url) ~ ("#" ~ branch)? }
short_url = ${ user ~ "/" ~ repo }
full_url  = ${ ((!"/" ~ ANY)+ ~ "/")+ ~ repo ~ ".git"? }

user   = @{ ident }
repo   = @{ ident }
branch = @{ ident ~ ("/" ~ ident)? }

attributes = _{ (";" ~ attribute)+ }
attribute  =  { attr_key ~ "=" ~ attr_val ~ WHITESPACE* }
attr_key   =  { ident }
attr_val   = @{ quoted_string | (WHITESPACE* ~ ("\\;" | (!(";" | WHITESPACE) ~ ANY)))+ }

ident = _{ (ASCII_ALPHA | ASCII_DIGIT | "_" | "-")+ }

// 
// --- Quoted String ----------------------------------------------------------
// 
quoted_string = { SOI ~ PUSH("\"" | "'") ~ quoted_inner ~ POP ~ EOI }

// This allows to escape double quote characters when inside a double-quoted
// string but does not allow escaping single quote characters inside a
// single-quoted string.
// To also allow escaping single quotes inside single-quoted strings, replace
// `("\\\"")` with `("\\" ~ PEEK)`.
quoted_inner = { (("\\\"") | (!PEEK ~ ANY))* }
